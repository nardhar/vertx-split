plugins {
    id 'application'
    id 'java'
    id 'idea'
    id "net.ltgt.apt" version "0.10"
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

subprojects {
    def vertxVersion = '3.5.4'
    def daggerVersion = '2.17'

    apply plugin: 'java'
    group = 'org.nardhar.vertxsplit'
    version = '1.0'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        compile "io.vertx:vertx-core:$vertxVersion"
        compile "io.vertx:vertx-web:$vertxVersion"
        compile "io.vertx:vertx-web-client:$vertxVersion"
        compile "io.vertx:vertx-mongo-client:$vertxVersion"
        compile "io.vertx:vertx-auth-jwt:$vertxVersion"
        compile "io.vertx:vertx-rabbitmq-client:$vertxVersion"
        compile "io.vertx:vertx-config:$vertxVersion"

        // Dagger
        annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
        compile "com.google.dagger:dagger:$daggerVersion"

        // MapStruct
        compile 'org.mapstruct:mapstruct-jdk8:1.2.0.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.2.0.Final'

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.4'
        annotationProcessor 'org.projectlombok:lombok:1.18.4'

        compile "joda-time:joda-time:2.10"

        compile "org.slf4j:slf4j-simple:1.7.25"
        compile "commons-io:commons-io:2.6"

        testCompile "junit:junit:4.12"
        testCompile "io.vertx:vertx-unit:$vertxVersion"
    }

    buildscript {
        repositories {
            jcenter()
        }

        dependencies {
        }
    }

    wrapper {
        gradleVersion = '4.10.3'
    }
}

project(':main') {
    dependencies {
        compile project(':app'),
            project(':domain-handler'),
            project(':domain-verticle'),
            project(':util'),
            project(':vertx'),
            project(':vertx-verticle')
    }

    mainClassName = 'io.vertx.core.Launcher'
    version = "1.0-SNAPSHOT"

    def mainVerticleName = 'org.nardhar.vertxsplit.MainVerticle'
    def watchForChange = 'src/**/*'
    def doOnChange = './gradlew classes'

    shadowJar {
      classifier = 'fat'
      manifest {
          attributes "Main-Verticle": mainVerticleName
      }
      mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
      }
    }

    run {
        args = ['run', mainVerticleName, "-conf=src/main/resources/config.json", "--launcher-class=$mainClassName"]
    }

    assemble.dependsOn installDist
    build.mustRunAfter clean
}

project(':domain-verticle') {
    dependencies {
        compile project(':app'), project(':domain-handler'), project(':vertx'), project(':vertx-verticle')
    }
}

project(':domain-handler') {
    dependencies {
        compile project(':app'), project(':vertx'), project(':vertx-verticle')
    }
}

project(':app') {
    dependencies {
        compile project(':vertx'), project(':vertx-verticle')
    }
}

project(':util') {
    dependencies {
        compile project(':vertx')
    }
}

project(':vertx-verticle') {
    dependencies {
        compile project(':vertx')
    }
}

project(':vertx') {
    dependencies {
    }
}
